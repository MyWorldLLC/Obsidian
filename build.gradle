def getGitVersion(){
    "git describe --tags --always --first-parent".execute().text.trim()
}

def getGitCredentials(){
    def process = "git credential fill".execute()
    def stream = new PrintStream(process.getOutputStream(), true)
    stream.println("url=https://github.com/")
    stream.close()

    process.text.trim().split("\n").collectEntries {it.split("=")}
}

def getGitUser(){
    System.getenv("GITHUB_USER") ?: gitCredentials["username"]
}

def getGitPassword(){
    System.getenv("GITHUB_TOKEN") ?: gitCredentials["password"]
}

def getPlatform(){
    def platform = System.getProperty('os.name').toLowerCase()
    if(platform.contains('win')){
        platform = 'win'
    }else if(platform.contains('mac')){
        platform = 'mac'
    }else if(platform.contains('linux')){
        platform = 'linux'
    }
    return platform
}

def getSkijaPlatform(){
    def platforms = [
            'win': 'windows',
            'mac': 'macos-x64',
            'linux': 'linux'
    ]
    platforms[platform]
}

def getLwjglPlatform(){
    def platforms = [
            'win': 'windows',
            'mac': 'macos',
            'linux': 'linux'
    ]
    platforms[platform]
}

group = 'com.myworldvw'
version = gitVersion

def generatePlatformConfig(platformProject, skijaName, lwjglName, obsidianName, jfxName = null){

    platformProject.apply plugin: 'java-library'
    platformProject.apply plugin: 'maven-publish'

    platformProject.dependencies {

        if(jfxName == null){
            api project(':Obsidian')
            implementation "io.github.humbleui:skija-$skijaName:0.105.0"
            implementation "org.lwjgl:lwjgl-yoga:3.3.1:natives-$lwjglName"
        }else{
            implementation project(":Platforms:Obsidian:${obsidianName - 'jfx-'}")
            implementation "org.openjfx:javafx-base:$jfxVersion:$jfxName"
            implementation "org.openjfx:javafx-graphics:$jfxVersion:$jfxName"
            implementation "org.openjfx:javafx-controls:$jfxVersion:$jfxName"
            implementation "org.openjfx:javafx-web:$jfxVersion:$jfxName"
            implementation "org.openjfx:javafx-media:$jfxVersion:$jfxName"
        }
    }

    platformProject.java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(jdkVersion)
        }
    }

    platformProject.publishing {
        publications {
            maven(MavenPublication) {

                groupId = project.group
                artifactId = 'obsidian' + "-$obsidianName"
                version = project.version

                from platformProject.components.java
            }
        }
        repositories {
            mavenLocal()
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/MyWorldLLC/Packages")
                credentials {
                    username = gitUser
                    password = gitPassword
                }
            }
        }
    }
}

allprojects{
    ext{
        jdkVersion = '17'
        jfxVersion = '17'
        lwjglPlatform = this.lwjglPlatform
        skijaPlatform = this.skijaPlatform
        platform = this.platform
        generatePlatformConfig = this.&generatePlatformConfig
        gitVersion = this.gitVersion
        gitCredentials = this.gitCredentials
        gitUser = this.gitUser
        gitPassword = this.gitPassword
    }
    repositories {
        mavenCentral()
        maven {
            name = "Chipmunk"
            url = uri("https://maven.pkg.github.com/MyWorldLLC/Chipmunk")
            credentials {
                username = gitUser
                password = gitPassword
            }
        }
    }
}